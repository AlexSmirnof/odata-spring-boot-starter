rootProject.version = readVersion()

// Gets version from Git tag.
def readVersion() {
  def output = new ByteArrayOutputStream()
  def result = exec {
    commandLine = ['git', 'describe']
    standardOutput = output
    ignoreExitValue = true
  }
  def version = (result.getExitValue() == 0) ?
      output.toString().trim() : 'UNKNOWN'
  println "Version ${version}"
  return version
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'signing'

  group = 'com.github.pukkaone'
  version = rootProject.version

  ext {
    olingoVersion = '4.3.0'
    springBootVersion = '1.4.0.RELEASE'
  }

  repositories {
    mavenLocal()
    mavenCentral()
  }

  sourceCompatibility = 1.8

  dependencies {
    compile "org.apache.olingo:odata-server-core-ext:${olingoVersion}"
    compileOnly 'org.projectlombok:lombok:1.16.16'
    testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
  }

  task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  artifacts {
    archives javadocJar
    archives sourcesJar
  }

  if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
    signing {
      sign configurations.archives
    }

    uploadArchives {
      repositories {
        mavenDeployer {
          beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

          repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
          }

          pom.project {
            name project.name
            packaging 'jar'
            description project.description
            url 'https://github.com/pukkaone/odata-spring-boot-starter'

            scm {
              connection 'scm:git:git@github.com:pukkaone/odata-spring-boot-starter.git'
              developerConnection 'scm:git:git@github.com:pukkaone/odata-spring-boot-starter.git'
              url 'https://github.com/pukkaone/odata-spring-boot-starter'
            }

            licenses {
              license {
                name 'Apache License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0'
                distribution 'repo'
              }
            }

            developers {
              developer {
                id 'pukkaone'
                name 'Chin Huang'
              }
            }
          }
        }
      }
    }
  }
}
